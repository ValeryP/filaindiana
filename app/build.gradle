apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

    defaultConfig {
        applicationId "com.codaliscia"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 16
        versionName "2.14"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }

        resConfigs "en", "it", "de"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            versionNameSuffix ".${gitCommitCount}"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix ".${gitCommitCount}"
            applicationIdSuffix '.dev'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'io.nlopez.smartlocation:library:3.3.3'
    implementation 'net.danlew:android.joda:2.10.3'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta5"
    implementation 'com.google.maps.android:android-maps-utils:1.2.1'
    implementation 'io.coil-kt:coil:0.10.1'
    implementation "androidx.room:room-runtime:2.2.5"
    implementation 'com.google.firebase:firebase-analytics:17.4.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'
    implementation "androidx.room:room-ktx:2.2.5"
    implementation 'br.com.simplepass:loading-button-android:2.2.0'
    implementation "androidx.work:work-runtime-ktx:2.3.4"
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
    implementation 'com.github.wooplr:Spotlight:1.2.3'
    implementation 'com.github.warkiz.widget:indicatorseekbar:2.1.2'
    implementation 'com.google.firebase:firebase-perf:19.0.7'

    kapt "androidx.room:room-compiler:2.2.5"

    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.room:room-testing:2.2.5"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'android.arch.persistence.room:testing:1.1.1'
    testImplementation 'androidx.arch.core:core-runtime:2.1.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    androidTestUtil 'androidx.test:orchestrator:1.2.0'
}
